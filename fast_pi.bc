#! /usr/bin/bc -q
# by pts@fazekas.hu at Sat Jul 17 17:38:25 CEST 2010

/*
a numdigits.a
b numdigits.b
c
d pi.nd
e numdigits.pa
f numdigits.pb
g
h
i
j
k
l
m
n
o
p acot.xpower
q
r
s acot.sum
t acot.term
u acot.u
v numdigits.v
w acot.xx
x acot.x
y pi.y
z pi.u
*/

define acot(x, u) {
  auto s, p, w, n, t
  s = p = u / x
  w = x * x
  n = 3
  for (;;) {
    p /= w
    t = p / n
    if (t == 0)
      return s
    s -= t
    n += 2
    p /= w
    t = p / n
    if (t == 0)
      return s
    s += t
    n += 2
  }
}

define numdigits(v) {
  auto n, a, b, e, f
  n = 1
  while (v > 9) {
    a = b = 1
    e = f = 10
    while (v > f) {
      a = b
      e = f
      f *= f
      b *= 2
    }
    f = e
    b = a
    while (v > f) {
      a = b
      e = f
      f *= 10
      b += 1
    }
    v /= e
    n += a
  }
  return n
}

define pi(d) {
  auto z, y
  z = 10 ^ d
  y = 4 * (4 * acot(5, z) - acot(239, z))
  y /= 10 ^ (numdigits((286135312 * d + 41739380) / 10000000) - 1)
  while (y % 10 == 0) {
    y /= 10
  }
  return y / 10
}

define pinfinite() {
  auto b, nb, nd, a, na
  print "3."
  b = 3
  nb = 1
  nd = 8
  for (;;) {
    a = pi(nd)
    na = numdigits(a)
    /* Print digits not printed yet in previous iterations. */
    print a % (10 ^ (na - nb))
    b = a
    nb = na
    nd *= 3
  }
}

pinfinite()  /* Infinite loop. */
